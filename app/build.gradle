apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/'}
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ru.movista"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 14
        versionName "2.1.1"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'RECAPTCHA_SITE_KEY', '"recaptcha_key"')
    }

    signingConfigs {
        release {
            storeFile file("movista_keystore.jks")
            storePassword "you_password"
            keyAlias "key_alias"
            keyPassword "you_password2"
        }
    }

    buildTypes {
        debug {
            debuggable = true
            ext.enableCrashlytics = false
            manifestPlaceholders = [
                    appIcon             : "@mipmap/ic_launcher_dev",
                    appIconRound        : "@mipmap/ic_launcher_dev_round",
                    screenOrientation   : "unspecified",
                    usesCleartextTraffic: "true",
                    googleMapsApiKey    : "_api_key_"
            ]
            buildConfigField('String', 'API_BASE_URL', '"https://api.maas.movista.ru/api/"') // prod
            buildConfigField('String', 'YANDEX_METRICA_API_KEY', '"_api_key_"')
            buildConfigField('String', 'REPORT_EMAIL_ADDRESS', '"support@movista.ru"')
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
        }
        stage {
            debuggable = false
            manifestPlaceholders = [
                    appIcon             : "@mipmap/ic_launcher_test",
                    appIconRound        : "@mipmap/ic_launcher_test_round",
                    screenOrientation   : "portrait",
                    usesCleartextTraffic: "false",
                    googleMapsApiKey    : "_api_key_"
            ]
            buildConfigField('String', 'API_BASE_URL', '"https://maasapi.stage-k8s.movista.ru/api/"')
            buildConfigField('String', 'YANDEX_METRICA_API_KEY', '"_key_"')
            buildConfigField('String', 'REPORT_EMAIL_ADDRESS', '"support@movista.ru"')
            signingConfig signingConfigs.release
            applicationIdSuffix ".test"
            versionNameSuffix "-stage"
        }
        release {
            minifyEnabled false
            debuggable = false
            manifestPlaceholders = [
                    appIcon             : "@mipmap/ic_launcher",
                    appIconRound        : "@mipmap/ic_launcher_round",
                    screenOrientation   : "portrait",
                    usesCleartextTraffic: "false",
                    googleMapsApiKey    : "_key_"
            ]
            buildConfigField('String', 'API_BASE_URL', '"https://api.maas.movista.ru/api/"')
            buildConfigField('String', 'YANDEX_METRICA_API_KEY', '"app_metrica_key"')
            buildConfigField('String', 'REPORT_EMAIL_ADDRESS', '"support@movista.ru"')
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Movista_${variant.buildType.name}_${defaultConfig.versionName}(${defaultConfig.versionCode}).apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    def maps_version = '17.0.0'
    def location_version = '17.0.0'
    def moxy_version = '1.0.13'
    def daggerVersion = '2.24'
    def retrofitVersion = '2.6.1'
    def okhttpVersion = "4.2.1"
    def anko_version = '0.10.8'
    def timber_version = '4.7.1'
    def moshi_version = '1.8.0'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.browser:browser:1.2.0'

    // Pictures
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.caverock:androidsvg-aar:1.4'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //Maps
    implementation "com.google.android.gms:play-services-maps:$maps_version"
    implementation "com.google.android.gms:play-services-location:$location_version"
    implementation 'com.google.maps.android:android-maps-utils:0.5+'
    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"

    //Moxy MVP
    implementation "com.github.moxy-community:moxy:$moxy_version"
    kapt "com.github.moxy-community:moxy-compiler:$moxy_version"
    implementation "com.github.moxy-community:moxy-androidx:$moxy_version"
    implementation "com.github.moxy-community:moxy-material:$moxy_version"

    //Cicerone navigation
    implementation 'ru.terrakok.cicerone:cicerone:5.0.0'

    //Dagger2
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    //Squareup
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"

    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding-material:3.0.0'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.0"

    //Timber Logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Threeten Date/Time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    // Moshi json
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Permissions Dispatcher
    implementation "org.permissionsdispatcher:permissionsdispatcher:4.5.0"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:4.5.0"

    // Lottie animation
    implementation 'com.airbnb.android:lottie:3.0.7'

    // KTX
    implementation 'androidx.core:core-ktx:1.1.0'

    // Other
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation 'com.redmadrobot:input-mask-android:4.3.1'
    implementation 'com.github.kizitonwose:CalendarView:0.2.6'
    implementation 'xyz.peridy.shimmer:shimmerlayout:1.1'

    // Analytics
    implementation 'com.yandex.android:mobmetricalib:3.6.4'
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.appsflyer:af-android-sdk:4+@aar'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') { transitive = true }

    implementation 'com.google.android:flexbox:1.0.0'

    implementation 'io.supercharge:shimmerlayout:2.1.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    // tests
    testImplementation 'junit:junit:4.12'
}

// Add the following line to the bottom of the file:
//apply plugin: 'com.google.gms.google-services'
